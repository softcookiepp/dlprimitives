
#ifndef COMMON_GLSL
#define COMMON_GLSL
// =================================================================================================
#extension GL_EXT_control_flow_attributes : enable
#define USE_BDA 0
#ifndef dtype
	// Parameters set by the tuner or by the database. Here they are given a basic default value in case
	// this file is used outside of the CLBlast library.
	#ifndef PRECISION
		#define PRECISION 32			// Data-types: half, single or double precision, complex or regular
	#endif

	// =================================================================================================
		
	// reserved for when unrolling semantics are able to be used
	#ifndef UNROLL
		#define UNROLL(N)
	#endif

	// Enable support for half-precision
	#if PRECISION == 16
		#extension GL_EXT_shader_16bit_storage : require
		#extension GL_EXT_shader_explicit_arithmetic_types_float16 : require
	#endif

	// Enable support for double-precision
	#if PRECISION == 64 || PRECISION == 6464
		#extension GL_EXT_shader_explicit_arithmetic_types_float64 : require
	#endif


	// Half-precision
	#if PRECISION == 16
		struct vec2_t { f16vec2 s; };
		struct vec4_t { f16vec4 s; };
		struct vec8_t { float16_t s[8]; };
		struct vec16_t { float16_t s[16]; };
		#define dtype float16_t
		#define ZERO float16_t(0.0)
		#define ONE float16_t(1.0)
		#define SMALLEST -1.0e14
		#define INFINITY float16_t(uintBitsToFloat(0x7F800000))
		#define NAN float16_t(uintBitsToFloat(0x7FC00000))
		#define PI float16_t(3.14159265358979323846)

	// Single-precision
	#elif PRECISION == 32
		struct vec2_t { vec2 s; };
		struct vec4_t { vec4 s; };
		struct vec8_t { float s[8]; };
		struct vec16_t { float s[16]; };
		#define dtype float
		#define ZERO dtype(0.0f)
		#define ONE 1.0f
		#define SMALLEST -1.0e37f
		#define INFINITY uintBitsToFloat(0x7F800000)
		#define NAN uintBitsToFloat(0x7FC00000)
		#define PI float(3.14159265358979323846)

	// Double-precision 
	#elif PRECISION == 64
		struct vec2_t { dvec2 s; };
		struct vec4_t { dvec4 s; };
		struct vec8_t { double s[8]; };
		struct vec16_t { double s[16]; };
		#define dtype double
		#define ZERO 0.0
		#define ONE 1.0
		#define SMALLEST -1.0e37
		#define INFINITY double(uintBitsToFloat(0x7F800000))
		#define NAN double(uintBitsToFloat(0x7FC00000))
		#define PI double(3.14159265358979323846)

	// Complex single-precision
	#elif PRECISION == 3232
		struct vec2_t { mat2x2 s; };
		struct vec4_t { mat4x2 s; };
		struct vec8_t { vec2 s[8]; };
		struct vec16_t { vec2 s[16]; };
		#define dtype vec2
		#define ZERO 0.0f
		#define ONE 1.0f
		#define SMALLEST -1.0e37f
		#define INFINITY uintBitsToFloat(0x7F800000)
		#define NAN uintBitsToFloat(0x7FC00000)
		#define PI float(3.14159265358979323846)

	// Complex double-precision
	#elif PRECISION == 6464
		struct vec2_t { dmat2x2 s; };
		struct vec4_t { dmat4x2 s; };					 
		struct vec8_t { dvec2 s[8]; };
		struct vec16_t { dvec2 s[16]; };
		#define dtype dvec2
		#define ZERO 0.0
		#define ONE 1.0
		#define SMALLEST -1.0e37
		#define INFINITY double(uintBitsToFloat(0x7F800000))
		#define NAN double(uintBitsToFloat(0x7FC00000))
		#define PI double(3.14159265358979323846)
	#endif

	// this simplifies stuff c:
	#define dtype2 vec2_t
	#define dtype4 vec4_t
	#define dtype8 vec8_t
	#define dtype16 vec16_t

	// Single-element version of a complex number
	#if PRECISION == 3232
		#define singledtype float 
	#elif PRECISION == 6464
		#define singledtype double 
	#else
		#define singledtype dtype 
	#endif

	// Converts a 'dtype argument' value to a 'dtype' value as passed to the kernel. Normally there is no
	// conversion, but half-precision is not supported as kernel argument so it is converted from float.
	#if PRECISION == 16
		#define dtype_arg float 
		#define GetRealArg(x) float16_t(x)
	#else
		#define dtype_arg dtype 
		#define GetRealArg(x) dtype(x)
	#endif

	//#elif PRECISION == 64
	//	// lets see if this makes our life easier...
	//	#define dtype_arg float
	//	#define GetRealArg(x) double(x)

	// Pointers to local memory objects (using a define because CUDA doesn't need them)
	#ifndef LOCAL_PTR
		#define LOCAL_PTR shared
	#endif

	// =================================================================================================

	// Don't use the non-IEEE754 compliant OpenCL built-in mad() instruction per default. For specific
	// devices, this is enabled (see src/routine.cpp).
	#ifndef USE_CL_MAD
		#define USE_CL_MAD 0
	#endif

	// By default the workgroup size requirement is enabled. For Qualcomm devices the workgroup size 
	// requirement results in worse performance and is disabled (src/utilities/compile.cpp)
	#ifndef RELAX_WORKGROUP_SIZE
		#define RELAX_WORKGROUP_SIZE 0
	#endif

	// ensure all spec constants related to workgroup size are here and ready
	#if RELAX_WORKGROUP_SIZE
		layout(local_size_x_id = 0, local_size_y_id = 1, local_size_z_id = 2) in;
	#endif

	// Sets a variable to zero
	#if PRECISION == 3232 || PRECISION == 6464
		#define SetToZero(a) a = dtype(ZERO, ZERO)
	#else
		#define SetToZero(a) a = ZERO
	#endif

	// Sets a variable to zero (only the imaginary part)
	#if PRECISION == 3232 || PRECISION == 6464
		#define ImagToZero(a) a.y = ZERO
	#else
		#define ImagToZero(a) 
	#endif

	// Sets a variable to one
	#if PRECISION == 3232 || PRECISION == 6464
		#define SetToOne(a) a = dtype(ONE, ZERO)
	#else
		#define SetToOne(a) a = ONE
	#endif

	// Determines whether a variable is zero
	#if PRECISION == 3232 || PRECISION == 6464
		#define IsZero(a) ((a[0] == ZERO) && (a[1] == ZERO))
	#else
		#define IsZero(a) (a == ZERO)
	#endif

	// component-wise absolute value
	#define AbsoluteValue(value) value = abs(value)

	// Negation (component-wise)
	#if 0 //PRECISION == 3232 || PRECISION == 6464
		#define Negate(value) value.x = (-1.0(value.x)); value.y = (-1.0*(value.y))
	#else
		#define Negate(value) value = (-1.0*(value))
	#endif

	// Adds two complex variables
	#if 0 //PRECISION == 3232 || PRECISION == 6464
		#define Add(c,a,b) c = dtype(a.x + b.x, a.y + b.y)
	#else
		#define Add(c,a,b) c = a + b
	#endif

	// Subtracts two complex variables
	#if 0 //PRECISION == 3232 || PRECISION == 6464
		#define Subtract(c,a,b) c = dtype(a.x - b.x, a.y - b.y)
	#else
		#define Subtract(c,a,b) c = a - b
	#endif

	// Multiply two complex variables (used in the defines below)
	#if PRECISION == 3232 || PRECISION == 6464
		#define MulReal(a,b) a[0]*b[0] - a[1]*b[1]
		#define MulImag(a,b) a[0]*b[1] + a[1]*b[0]
	#endif

	// The scalar multiply function
	#if PRECISION == 3232 || PRECISION == 6464
		#define Multiply(c,a,b) c = dtype(MulReal(a,b), MulImag(a,b))
	#else
		#define Multiply(c,a,b) c = a * b
	#endif

	#define vMultiply(c, a, b, vWidth) \
	{ \
		UNROLL(vWidth) \
		for (uint i = 0; i < vWidth; i += 1) \
		{ \
			Multiply(c.s[i], a.s[i], b.s[i]); \
		} \
	}

	// c is vector, a is scalar, b is vector
	#define vsMultiply(c, a, b, vWidth) \
	{ \
		UNROLL(vWidth) \
		for (uint i = 0; i < vWidth; i += 1) \
		{ \
			Multiply(c.s[i], a, b.s[i]); \
		} \
	}

	// c is vector, a is scalar, b is vector
	#define vsMultiplyAdd(c, a, b, vWidth) \
	{ \
		UNROLL(vWidth) \
		for (uint i = 0; i < vWidth; i += 1) \
		{ \
			MultiplyAdd(c.s[i], a, b.s[i]); \
		} \
	}

	#define vSetToZero(v, vWidth) \
	{ \
		UNROLL(vWidth) \
		for (uint i = 0; i < vWidth; i += 1) \
		{ \
			SetToZero(v.s[i]); \
		} \
	}

	// The scalar multiply-add function
	#if PRECISION == 3232 || PRECISION == 6464
		#define MultiplyAdd(c,a,b) c += dtype(MulReal(a,b), MulImag(a,b))
	#else
		#if 0 //USE_CL_MAD == 1
			#define MultiplyAdd(c,a,b) c = mad(a, b, c)
		#else
			#define MultiplyAdd(c,a,b) c += (a * b)
		#endif
	#endif

	// The scalar multiply-subtract function
	#if PRECISION == 3232 || PRECISION == 6464
		#define MultiplySubtract(c,a,b) c -= dtype(MulReal(a,b), MulImag(a,b))
	#else
		#define MultiplySubtract(c,a,b) c -= (a * b)
	#endif

	// The scalar division function: full division
	#if PRECISION == 3232 || PRECISION == 6464
		#define DivideFull(c,a,b) singledtype num_x = (a.x * b.x) + (a.y * b.y); singledtype num_y = (a.y * b.x) - (a.x * b.y); singledtype denom = (b.x * b.x) + (b.y * b.y); c = dtype(num_x / denom, num_y / denom)
	#elif PRECISION == 16
		// Some hardware doesn't compute NaN properly
		// #define DivideFull(c,a,b) c = (b == ZERO ? NAN : a / b)
		// still need to test the specifics.
		// until then, just use default behavior...
		#define DivideFull(c,a,b) c = a / b
	#else
		#define DivideFull(c,a,b) c = a / b
	#endif

	// The scalar AXPBY function
	#if PRECISION == 3232 || PRECISION == 6464
		//#define AXPBY(e,a,b,c,d) e.x = MulReal(a,b) + MulReal(c,d); e.y = MulImag(a,b) + MulImag(c,d)
		#define AXPBY(e,a,b,c,d) e = dtype(MulReal(a,b) + MulReal(c,d), MulImag(a,b) + MulImag(c,d))
	#else
		#define AXPBY(e,a,b,c,d) e = a*b + c*d
	#endif

	// The complex conjugate operation for complex transforms
	#if PRECISION == 3232 || PRECISION == 6464
		#define COMPLEX_CONJUGATE(value) value.y = -1.0*value.y
	#else
		#define COMPLEX_CONJUGATE(value) 
	#endif
#endif
// =================================================================================================

#endif
//)"

// =================================================================================================
